<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="50" height="50"
    borderStyle="solid" borderColor="0x333333" clipContent="true"
    horizontalScrollPolicy="off" verticalScrollPolicy="off" initialize="init()"
    mouseOver="handleMouseOver(event)" rollOut="handleMouseOut(event)">
<mx:Script>
    <![CDATA[
        [Embed("assets/icons/overlay_reply_up.png")]
        private const replyUpSkin:Class;

        [Embed("assets/icons/overlay_reply_alt.png")]
        private const replyAltSkin:Class;

        [Embed("assets/icons/overlay_dm_up.png")]
        private const directMessageUpSkin:Class;

        [Embed("assets/icons/overlay_dm_alt.png")]
        private const directMessageAltSkin:Class;

        [Embed("assets/icons/overlay_lookup_up.png")]
        private const lookupUpSkin:Class;

        [Embed("assets/icons/overlay_lookup_alt.png")]
        private const lookupAltSkin:Class;

        [Embed("assets/icons/overlay_follow_up.png")]
        private const followUpSkin:Class;

        [Embed("assets/icons/overlay_follow_alt.png")]
        private const followAltSkin:Class;

        [Embed("assets/icons/overlay_unfollow_up.png")]
        private const unfollowUpSkin:Class;

        [Embed("assets/icons/overlay_unfollow_alt.png")]
        private const unfollowAltSkin:Class;

        [Bindable]
        private var _source:String;

        [Bindable]
        private var _following:Boolean;

        public static const REPLY:String = "reply";

        public static const DIRECT_MESSAGE:String = "directMessage";

        public static const LOOKUP:String = "lookup";

        public static const FOLLOW:String = "follow";

        public static const UNFOLLOW:String = "unfollow";

        public function set source(value:String):void {
            _source = value;
        }

        public function set following(value:Boolean):void {
            _following = value;
            setFollowUnfollowButtonSkin();
        }

        private function init():void {
            currentState = "normal";
        }

        private function handleMouseOver(event:MouseEvent):void {
            currentState = "hovered";
        }

        private function handleMouseOut(event:MouseEvent):void {
            currentState = "normal";
        }

        private function handleComplete(event:Event):void {
            var bitmap:Bitmap = Bitmap(Image(event.currentTarget).content);
            if (bitmap != null) {
                bitmap.smoothing = true;
            }
        }

        private function handleReplyClick(event:MouseEvent):void {
            dispatchEvent(new Event(REPLY));
        }

        private function handleDirectMessageClick(event:MouseEvent):void {
            dispatchEvent(new Event(DIRECT_MESSAGE));
        }

        private function handleLookupClick(event:MouseEvent):void {
            dispatchEvent(new Event(LOOKUP));
        }

        private function handleFollowUnfollowClick(event:MouseEvent):void {
            dispatchEvent(new Event((_following) ? UNFOLLOW : FOLLOW));
        }

        protected override function childrenCreated():void {
            super.childrenCreated();

            // Set the icons
            replyBtn.setStyle("upSkin", replyUpSkin);
            replyBtn.setStyle("overSkin", replyAltSkin);
            replyBtn.setStyle("downSkin", replyUpSkin);

            directMsgBtn.setStyle("upSkin", directMessageUpSkin);
            directMsgBtn.setStyle("overSkin", directMessageAltSkin);
            directMsgBtn.setStyle("downSkin", directMessageUpSkin);

            lookupBtn.setStyle("upSkin", lookupUpSkin);
            lookupBtn.setStyle("overSkin", lookupAltSkin);
            lookupBtn.setStyle("downSkin", lookupUpSkin);

            setFollowUnfollowButtonSkin();
        }

        private function setFollowUnfollowButtonSkin():void {
            if (followUnfollowBtn != null) {
                if (_following) {
                    followUnfollowBtn.setStyle("upSkin", unfollowUpSkin);
                    followUnfollowBtn.setStyle("overSkin", unfollowAltSkin);
                    followUnfollowBtn.setStyle("downSkin", unfollowUpSkin);
                    followUnfollowBtn.toolTip = "Unfollow";
                } else {
                    followUnfollowBtn.setStyle("upSkin", followUpSkin);
                    followUnfollowBtn.setStyle("overSkin", followAltSkin);
                    followUnfollowBtn.setStyle("downSkin", followUpSkin);
                    followUnfollowBtn.toolTip = "Follow";
                }
            }
        }
    ]]>
</mx:Script>
<mx:states>
    <mx:State name="normal">
        <mx:SetProperty target="{imageOverlay}" name="visible" value="false" />
    </mx:State>
    <mx:State name="hovered">
        <mx:SetProperty target="{imageOverlay}" name="visible" value="true" />
    </mx:State>
</mx:states>
<mx:Image width="100%" height="100%" complete="handleComplete(event)" source="{_source}" x="0" y="0" />
<mx:VBox id="imageOverlay" x="0" y="0" verticalGap="2" backgroundColor="#000000" backgroundAlpha="0.3"
    paddingLeft="2" paddingTop="3" horizontalScrollPolicy="off" verticalScrollPolicy="off" width="100%" height="100%"
    visible="false">
    <mx:HBox horizontalGap="2">
        <mx:LinkButton id="replyBtn" toolTip="Reply" click="handleReplyClick(event)" />
        <mx:LinkButton id="directMsgBtn" toolTip="Direct Message" click="handleDirectMessageClick(event)" />
    </mx:HBox>

    <mx:HBox paddingBottom="2" horizontalGap="2">
        <mx:LinkButton id="lookupBtn" toolTip="Lookup" click="handleLookupClick(event)" />
        <mx:LinkButton id="followUnfollowBtn" toolTip="Follow" click="handleFollowUnfollowClick(event)" />
    </mx:HBox>
</mx:VBox>
</mx:Canvas>
